package it.twopay.entity;

import java.util.Arrays;
import java.util.Map;

/* =================
PRE CHECK

Set the state to "checked" if .perform has been called with all of:
- CHECK_MIRROR, RELEASE_PARKING_BRAKE, FASTEN_SEATBELT

*/
rule "Rule: pre check"
ruleflow-group "precheck"
when
    $car: Car($actions: actions)
    $action: Car.Action(
        command in (CarCommands.CHECK_MIRROR, CarCommands.RELEASE_PARKING_BRAKE, CarCommands.FASTEN_SEATBELT)
        && state == 'init'
    ) from $actions
then
    $car.log.info("### Rule: precheck for " + $action.getCommand().name());
    modify($action) {
        setState("checked")
    }
    modify($car) {
        setState("checked")
    }
end

/* =================
Auto Operations

If .perform called with START_TRIP, perform FILL_UP_TANK

*/
rule "Rule: auto operations"
ruleflow-group "auto-ops"
when
	$car: Car()
	$action: Car.Action(command == CarCommands.START_TRIP && state == 'init') from $car.actions
then
	$car.log.info("### Rule: Rule: auto operation");	
	modify($action) {
		setState("checked")
	}
	modify($car) {
		perform(CarCommands.FILL_UP_TANK)
	}
end


/* =================
Post Check -- Report error when applicable
*/
rule "Rule: report precheck errors"
ruleflow-group "postcheck"
when
	$car: Car(
		state == "checked" &&
		stateCount != 3
	)
then
	$car.log.info("### Rule: report precheck errors");
	for (CarCommands command : Arrays.asList(CarCommands.CHECK_MIRROR, CarCommands.RELEASE_PARKING_BRAKE, CarCommands.FASTEN_SEATBELT)) {
		if (!$car.getCommands().contains(command)) {
			String error = "Missing action " + command.name();
			$car.addError(error);
		}
	};
	modify($car) {
        setState("error")
    }
end

/*
Allow car start onloy if .perform was called with FILL_UP_TANK
*/
rule "Rule: allow the car to start"
ruleflow-group "postcheck"
when
	$car: Car(
		state == "checked" &&
		stateCount == 3
	)
	$action: Car.Action(command == CarCommands.FILL_UP_TANK && state == 'init') from $car.actions
then
	$car.log.info("### Rule: allow the car to start");
	modify($action) {
		setState("checked")
	}
	modify($car) {
        setState("allow-start")
    }
end


